# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20240922
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=('any')
url='https://www.gnu.org/software/parallel/'
license=('GPL-3.0-only')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('6db45bcb61a00bd6699e7a97cc9ad342c864689f81883051dba9dc9d5356be84d8fa913ee3c07461ba3724da9ff4d554e99cf70481b6c6367e65eb708e01da05'
            'SKIP'
            '9ea7d85814daa57d69157f4d1819c34b21cf31d3acf7a7d0bce3a2fbaa1761f2e34193d3ec7c31396e3cfb0525137e7ed8fc0f8a045fda05c81827483ac50003')
b2sums=('f1efb6d45496c694afabcdaf6273f1d45fddd59bf3f925708474702aef5ee062f09ca5e00715ab5da8ee7a8c3f3f28fee3c8582ce0214dc8bf588bb3a1adb122'
        'SKIP'
        '9edfac80826f8429ada7154d2afd8e710d44806283e01248fa447262966fd067835b4756ba3a78cb257bcd809b2e91eaeb1227956f1590135e8476fef6f5ff75')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:

